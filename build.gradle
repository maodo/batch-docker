buildscript {
    repositories {
        maven {
            url 'http://repo.spring.io/plugins-release'
            mavenCentral()
            mavenLocal()
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.3.RELEASE")
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

apply plugin: 'eclipse'
apply plugin: 'idea'

allprojects {

    repositories {
        mavenCentral()
        maven { url 'http://repo.spring.io/libs-milestone' }
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url "https://repo.spring.io/libs-snapshot" }
    }
}

subprojects { subproject ->

    apply plugin: 'java'
    apply plugin: 'spring-boot'

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    eclipse {
        project { natures += 'org.springframework.ide.eclipse.core.springnature' }
    }
}

project('job') {
    apply plugin: 'docker'

    bootRepackage {
        enabled = true
    }

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-batch")
        compile("org.springframework.boot:spring-boot-starter-mail")
        compile("org.springframework.boot:spring-boot-starter-jdbc")
        compile('mysql:mysql-connector-java')
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("junit:junit")
    }

    jar {
        baseName = 'batch'
    }

    group = 'mminella'

    task buildDocker(type: Docker, dependsOn: build) {
        push = true
        applicationName = jar.baseName
        dockerfile = file('src/main/docker/Dockerfile')
        doFirst {
            copy {
                from jar
                into stageDir
            }
        }
    }
}

project('orchestration') {

    dependencies {
        compile project(":docker-hub-client")
        compile("org.springframework.boot:spring-boot-starter-web")
        compile("org.springframework.boot:spring-boot-starter-thymeleaf")
        compile("org.springframework.boot:spring-boot-starter-jdbc")
        compile("io.pivotal.receptor:receptor-client:0.0.1-SNAPSHOT")
        compile('mysql:mysql-connector-java')
        compile('org.springframework.batch:spring-batch-admin-manager:1.3.0.RELEASE') {
            exclude group: 'org.slf4j', module: 'slf4j-log4j12'
            exclude group: 'org.slf4j', module: 'slf4j-api'
        }
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("junit:junit")
    }
}

project('docker-hub-client') {

    bootRepackage {
        enabled = false
    }

    dependencies {
        compile("com.fasterxml.jackson.core:jackson-databind:2.5.1")
        compile("org.springframework:spring-web:4.1.5.RELEASE")
        testCompile('org.mockito:mockito-core:1.9.5')
        testCompile("junit:junit:4.12")
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}